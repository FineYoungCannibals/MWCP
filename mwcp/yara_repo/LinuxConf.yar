rule SSHPublicKeyRule
{
	meta:
		description = "Identifies SSH pub key files"
		author = "FH"
		date = "2025-01-28"
		version = "1.0"
		mwcp = "LinuxFinder.SSHPublicKey"

	strings:
        $ssh1re = /ssh\x2d[0-9a-zA-Z]+\x20/i
        $ssh2re = /ecdsa\x2d[0-9a-zA-Z]+\x2d[0-9a-zA-Z]+\x20/i
        $known_hosts = "|1|"
        $keygen = "keygen"
	condition:
        ($ssh1re or $ssh2re) and not ($known_hosts or $keygen)
}
rule SSHPrivateKeyRule
{
	meta:
		description = "Identifies SSH private key files"
		author = "FH"
		date = "2025-01-30"
		version = "1.0"
		mwcp = "LinuxFinder.SSHPrivateKey"

	strings:
        $string1 = /\-{5}BEGIN\x20[^\x20]+\x20PRIVATE\x20KEY\-{5}/
        $string2 = /\-{5}END\x20[^\x20]+\x20PRIVATE\x20KEY\-{5}/
	condition:
        all of them
}
rule KnownHostsRule
{
	meta:
		description = ""
		author = "FH"
		date = "2025-01-28"
		version = "1.0"
		mwcp = "LinuxFinder.KnownHosts"

	strings:
        $ssh1re = /ssh\x2d[0-9a-zA-Z]+\x20/i
        $ssh2re = /ecdsa\x2d[0-9a-zA-Z]+\x2d[0-9a-zA-Z]+\x20/i
        $known_hosts = "|1|"
	condition:
        ($ssh1re or $ssh2re) and $known_hosts
}
rule SSHConfigFileRule
{
	meta:
		description = ""
		author = "FH"
		date = "2025-01-28"
		version = "1.0"
		mwcp = "LinuxFinder.Sshconfig"
    strings:
        $host = /host\x20[^\s]/i
        $hostname = /HostName\x20[^\s]/
        $identityfile = /IdentityFile\x20[^\s]/
    condition:
        all of them
}
rule BashHistoryRule
{
       meta:
            description = "detect a bash history, pull commands and their parameters"
            author = "FH"
            date = "2025-01-28"
            version = "1.0"
            mwcp = "LinuxFinder.BashHistory"
       strings:
            $command1 = /echo [^\s]+/
            $command2 = /cat [^\s]+/
            $command3 = /usermod [^\s]+/
            $command4 = /su [^\s]+/
            $command5 = /source [^\s]+/
            $command6 = /(vim?|nano|emacs)\x20[^\s]+/
            $command7 = /passwd [^\s]+/
            $command8 = /adduser [^\s]+/
            $bashrc = /\# \~\/\.bashrc\: executed by bash\(1\) for non\-login shells\./
       condition:
            (#command1 + #command2 + #command3 + #command4 + #command5 + #command6 + #command7 + #command8) > 2 and not $bashrc
}
rule ViminfoRule
{
       meta:
            description = "detect a bash history, pull commands and their parameters"
            author = "FH"
            date = "2025-01-30"
            version = "1.0"
            mwcp = "LinuxFinder.Viminfo"
       strings:
            $viminfo1 = "This viminfo file was generated by Vim"
            $viminfo2 = "Viminfo version"
            $viminfo3 = "Last Search Pattern"
       condition:
           any of them
}
